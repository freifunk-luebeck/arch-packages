# Maintainer: Emil Renner Berthing <esmil@mailme.dk>

pkgname=tayga
pkgver=0.9.2
pkgrel=2
pkgdesc="Simple, no-fuss NAT64 for Linux"
arch=(i686 x86_64)
url="http://www.litech.org/tayga/"
license=('GPLv2')
backup=('etc/tayga.conf')
source=("http://www.litech.org/tayga/tayga-$pkgver.tar.bz2" "tayga.service")
depends=('glibc')
md5sums=('7a7b24165ce008df772f398d86fa280e' '34bc6aa93cce64f353760ba2435c3dd6')

build() {
  cd "$srcdir/tayga-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

check() {
  cd "$srcdir/tayga-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/tayga-$pkgver"
  make DESTDIR="$pkgdir/" install
  mv "$pkgdir/etc/tayga.conf.example" "$pkgdir/etc/tayga.conf"
  mkdir -p "$pkgdir/var/db/tayga"

  install -Dm644 "$srcdir/tayga.service" "${pkgdir}/usr/lib/systemd/system/tayga.service"
}

# vim:set ts=2 sw=2 et:
